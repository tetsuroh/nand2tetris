// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/Memory.hdl

/**
 * The complete address space of the Hack computer's memory,
 * including RAM and memory-mapped I/O. 
 * The chip facilitates read and write operations, as follows:
 *     Read:  out(t) = Memory[address(t)](t)
 *     Write: if load(t-1) then Memory[address(t-1)](t) = in(t-1)
 * In words: the chip always outputs the value stored at the memory 
 * location specified by address. If load==1, the in value is loaded 
 * into the memory location specified by address. This value becomes 
 * available through the out output from the next time step onward.
 * Address space rules:
 * Only the upper 16K+8K+1 words of the Memory chip are used. 
 * Access to address>0x6000 is invalid. Access to any address in 
 * the range 0x4000-0x5FFF results in accessing the screen memory 
 * map. Access to address 0x6000 results in accessing the keyboard 
 * memory map. The behavior in these addresses is described in the 
 * Screen and Keyboard chip specifications given in the book.
 */

CHIP Memory {
    IN in[16], load, address[15];
    OUT out[16];

    PARTS:
    // address of memory
    Not(in=address[14],
        out=ismem);
    And(a=load, b=ismem, out=ismemload);

    // address of keyboard
    Not16(in[0..1]=address[13..14], out[0]=nota13, out[1]=nota14);
    Or8Way(in=address[0..7], out=or1);
    Or8Way(in[0..4]=address[8..12], in[5]=nota13, in[6]=nota14,
           in[7]=or1, out=or2);
    Not(in=or2, out=iskeyboard);

    // address of screen
    Not(in=iskeyboard, out=isntkeyboard);
    And(a=address[14], b=isntkeyboard, out=isscreen);
    And(a=load, b=isscreen, out=isscreenload);

    
    RAM16K(in=in, load=ismemload, address=address[0..13], out=memout);

    Screen(in=in, load=isscreenload, address=address[0..12], out=screenout);
    Keyboard(out=keyboardout);

    Mux8Way16(b=memout, c=screenout, e=keyboardout,
              sel[0]=ismem, sel[1]=isscreen, sel[2]=iskeyboard, out=out);
}